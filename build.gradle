
buildscript {
    ext.kotlin_version = '1.1.0'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath group: 'org.junit.platform', name: 'junit-platform-gradle-plugin', version: '1.0.0-M3'
    }
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'

apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.github.erictsangx'
archivesBaseName = "kotlin-neo4j"
version = "0.0.3"

junitPlatform {
    filters {
        engines {
            include 'junit-jupiter'
        }
        includeClassNamePatterns '.*Test', '.*IT', '.*Spec'
    }
}


repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.slf4j:slf4j-api:1.7.21'

    //neo4j
    compile 'org.neo4j.driver:neo4j-java-driver:1.1.1'

    //junit5
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M3")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M3")

    testCompile "com.etx.test:kotlin-random:1.0.0"

    testCompile 'io.github.microutils:kotlin-logging:1.4'
    testCompile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.7'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
    testCompile "org.neo4j:neo4j-slf4j:3.1.0"
    testCompile "org.neo4j.test:neo4j-harness:3.1.0"

    testCompile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.4"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name  "${project.group}:${project.archivesBaseName}"
                packaging 'jar'
                description "kotlin-neo4j is a wrapper of 'org.neo4j.driver:neo4j-java-driver' and written by Kotlin."
                url "https://github.com/erictsangx/kotlin-neo4j"

                scm {
                    connection 'scm:git:git://github.com/erictsangx/kotlin-neo4j.git'
                    developerConnection 'scm:git:ssh://github.com/erictsangx/kotlin-neo4j.git'
                    url 'https://github.com/erictsangx/kotlin-neo4j'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'erictsangx'
                        name 'Eric Tsang'
                        email 'erictsangx@gmail.com'
                    }
                }
            }
        }
    }
}

